class ATC
types
public Point :: x:int
								y:int;
public Orientation = <N>|<NW>|<W>|<SW>|<S>|<SE>|<E>|<NE>;
values
-- TODO Define values here
instance variables

	-- Primary variables
	private airplanes: seq of Airplane;
	private runway: Runway;
	private beacons: seq of Point;
	private planeIDs: seq of nat;
	--Variable to check for warnings
	private warnings: map nat to nat;
	private colisions: map nat to nat;
	
	--Variable used to check if we lost
		private lost: int := 0;
		
	--Invariant for id of planes
	inv not exists p1, p2 in set elems airplanes &
		p1 <> p2 and p1.idPlane = p2.idPlane;
			
operations
	-- Constructor
	public ATC: nat * nat * Orientation * nat ==> ATC
	ATC(rx, ry, rori, c) ==
	(
		airplanes := [];
		runway := new Runway(rx, ry, rori, c);
		beacons := [mk_Point(-1,-1)];
		warnings := {|->};
		colisions := {|->};
		planeIDs := [];
	);
	
	-- Gets the seq of Airplane
	public getPlanes: () ==> seq of Airplane
	getPlanes() ==
	(
		return airplanes;
	);
	
	-- Adds an airplane
	public addPlane: Airplane ==> ()
	addPlane(p) ==
	(
		dcl pNew : Airplane := new Airplane(p.getID(), p.getPos().x , p.getPos().y , p.getOrientation(), p.getSpeed(), p.getGoal().x, p.getGoal().y);
		airplanes := [pNew] ^ airplanes;
		planeIDs := [p.getID()] ^ planeIDs;
	)
	post len airplanes = len airplanes~ +1;
	
	-- Get runway
	public getRunway: () ==> Runway
	getRunway() ==
	(
		return runway;
	);
	
	-- Get plane by id
	public getPlaneByID: nat ==> Airplane
	getPlaneByID(id) ==
	(
		
		let i in set inds planeIDs be st id = planeIDs(i) in return airplanes(i);
		
	)
	pre id in set elems planeIDs;
	
	-- Removes an airplane
	public removePlane: Airplane ==> ()
	removePlane(p) ==
	(
	let i in set inds airplanes be st p = airplanes(i) 
		in airplanes := airplanes (1,...,i-1) ^ airplanes(i+1,...,len airplanes);
	let i in set inds planeIDs be st p.getID() = planeIDs(i) 
		in planeIDs := planeIDs (1,...,i-1) ^ planeIDs (i+1,...,len planeIDs);
		
		
	)
	pre p in set elems airplanes
	post (not exists x in set elems airplanes & x = p) and len planeIDs = len planeIDs~ -1;  
	
	-- Removes a plane after it goes OutOfReach
	public removeOutOfReach: () ==> ()
	removeOutOfReach() ==
	(
			for all p in set elems airplanes do
			(
				if(p.getOutOfReach()) then removePlane(p);
			);
	);
	
	-- Adds a beacon
	public addBeacon: Point ==> ()
	addBeacon(b) ==
	(
		beacons := [b] ^ beacons;
	)
	pre not exists x in set elems beacons & x = b
	post beacons = [b] ^ beacons~;
	
	-- Gets all colisions
	public getColisions: () ==> map nat to nat
	getColisions() ==
	(
		return colisions;
	);
	
	-- Gets all warnings
	public getWarnings: () ==> map nat to nat
	getWarnings() ==
	(
		return warnings;
	);
	 
	-- Orders a plane to rotate clockwise
	public orderRotateCW: Airplane * int ==> ()
	orderRotateCW(p,r) ==
	(
		let i in set inds airplanes be st p = airplanes(i) 
			in airplanes(i).setTargetRotation(-r);
	) 
	pre (r rem 45) = 0 and p in set elems airplanes;
	
	-- Orders a plane to rotate counterclockwise
	public orderRotateCCW: Airplane * int ==> ()
	orderRotateCCW(p,r) ==
	(
		let i in set inds airplanes be st p = airplanes(i) 
			in airplanes(i).setTargetRotation(r);
	)
	pre (r rem 45) = 0 and p in set elems airplanes;
	
	-- Speed a plane up or down
	public orderSpeed: Airplane * real ==> ()
	orderSpeed(p,s) ==
	(
		let i in set inds airplanes be st p = airplanes(i) 
			in airplanes(i).setTargetSpeed(s);
	)
	pre s >= 0.1 and s <= 1.0 and p in set elems airplanes;
	
	-- Order a plane to go to a beacon
	public orderSecGoal: Airplane * Point ==> ()
	orderSecGoal(p,beacon) ==
	(
		let i in set inds airplanes be st p = airplanes(i) 
			in airplanes(i).setSecGoal(beacon.x, beacon.y);
	)
	pre beacon in set elems beacons and p in set elems airplanes;
	
	--Checks which planes are next to each other
	-- If there are no warnings there is no coverage inside the second third
	public checkWarning: () ==> ()
	checkWarning() ==
	(
		warnings := {|->};
		for p1 in airplanes do
		(
			if(not p1.getLanded() and not p1.getOutOfReach()) then
			(
				for all p2 in set elems airplanes do
				(
					if(p1 <> p2 and ({p2.getID()} <: warnings) <> {p2.getID() |-> p1.getID()}
						and not p2.getLanded() and not p2.getOutOfReach()) then
					(
						dcl p1x: int := p1.getPos().x;
						dcl p1y: int := p1.getPos().y;
						dcl p2x: int := p2.getPos().x;
						dcl p2y: int := p2.getPos().y;
						
						if((p1x = (p2x + 1) or p1x =( p2x - 1)) and
							(p1y = (p2y + 1) or p1y = (p2y - 1) or p1y = p2y)) then
						(
							warnings := warnings munion {p1.getID() |-> p2.getID()};						
						)
						else if((p1x = (p2x + 1) or p1x = (p2x - 1) or p1x = p2x)) and
							(p1y = (p2y + 1) or p1y = (p2y - 1)) then
						(
							warnings := warnings munion {p1.getID() |-> p2.getID()};	
						);
					);
				);
			);
		);
		
		if(card dom warnings > 0) then
		(
			IO`println("WARNING: Distance between planes");
			IO`println(warnings);
			
		);
	);
	
	-- Checks for colisions
	-- If there are no colisions there is no coverage inside the second third
	public checkColisions: () ==> ()
	checkColisions() ==
	(
		for p1 in airplanes do
		(
			if(not p1.getLanded() and not p1.getOutOfReach()) then
			(
				for all p2 in set elems airplanes do
				(
					if( p1 <> p2 and not p2.getLanded() and not p2.getOutOfReach()
						and ({p2.getID()} <: colisions) <> {p2.getID() |-> p1.getID()} ) then
					(
						dcl p1x: int := p1.getPos().x;
						dcl p1y: int := p1.getPos().y;
						dcl p2x: int := p2.getPos().x;
						dcl p2y: int := p2.getPos().y;
						
						if( p1x = p2x and p1y = p2y ) then
						(
							colisions := colisions munion {p1.getID() |-> p2.getID()};	
						);
					);
				);
			);
		);
	)
	post colisions = {|->};
	
	-- Land plane
	public landPlane: Airplane ==> ()
	landPlane(p) ==
	(
		p.setLanded();
		runway.addPlane(p);
	)
	pre exists p1 in set elems airplanes & p = p1;
	
	-- Moves every plane
	-- Full coverage is achieved only when the game is lost
	public move: () ==> int
	move() ==
	(
		dcl n : nat := 0;
		for all p in set elems airplanes do
		(
			if(not p.getLanded() and not p.getOutOfReach()) then
			(
				n := p.move();
				if( n = 2 or n = 3) then
				(
					if(p.getGoal().x = runway.getPos().x and p.getGoal().y = runway.getPos().y and p.getOrientation() = runway.getOrientation() and p.getSpeed() < 0.3) then
					(
						landPlane(p);
					);
				)
				else if( n = 4 ) then
				(
					lost := 1;
					return n;
				)
			);
		);
		
		return 0;
	)
	post lost <> 1;
	
	-- Checks if a airplane exists in a position
	-- Used when printBoard is used
	-- Full coverage if there is a plane
	private existsPlanePos: nat * nat ==> bool
	existsPlanePos(x,y) ==
	(
		for all p in set elems airplanes do
		(
			if(p.getPos() = mk_Airplane`Point(x,y)) then
				return true;
		);
		return false;
	)
	pre x >= 0 and x < 20 and y >= 0 and y < 20;
	
	-- Checks if a beacon exists in a position
	-- Used when printBoard is used
	-- Full coverage if there is a beacon
	private existsBeaconPos: nat * nat ==> bool
	existsBeaconPos(x,y) ==
	(
		for all b in set elems beacons do
		(
			if(b = mk_Point(x,y)) then
				return true;
		);
		return false;
	)
	pre x >= 0 and x < 20 and y >= 0 and y < 20;
	
	-- Function to print the current board;
	public printSpace: () ==> ()
	printSpace() == 
	(
		IO`print("y|x, 0 , 1 , 2 , 3 , 4 , 5 , 6 , 7 , 8 , 9 , 10, 11, 12, 13, 14, 15, 16, 17, 18, 19,");  
		for i = 0 to 19 do
		(
			IO`print("\n ");
			IO`print(i);
			if( i < 10) then IO`print(" ");
			
			for j = 0 to 19 do
			(
				if(runway.getPos() = mk_Runway`Point(i,j)) then
				(
					
					IO`print(", R ");
					if(j = 19) then IO`print(",");
				)
				else if(existsPlanePos(i,j)) then
				(
					IO`print(", A ");
					if(j = 19) then IO`print(",");
				)
				else if(existsBeaconPos(i,j)) then
				(
					IO`print(", B ");
					if(j = 19) then IO`print(",");
				)
				else
				( 
					IO`print(",   ");
					if(j = 19) then IO`print(",");		
				)
			);
		);
	);
	-- Main function
	public main: () ==> ()
	main() ==
	(
		dcl n : nat := 0;
		
		n := move();
		checkColisions();
		checkWarning();
		removeOutOfReach();
	);
	
	
	
functions
-- TODO Define functiones here
traces
-- TODO Define Combinatorial Test Traces here
end ATC